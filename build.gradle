/*
 * Copyright 2019 Andrey S Teplitskiy
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "java"
    id "maven"
    id "maven-publish"
    id "com.gradle.plugin-publish" version "0.10.1"
    id "com.jfrog.bintray" version "1.8.4"
    id "com.chmyaf.gradle.plugin.changelog" version "1.0.0"
    id "com.chmyaf.gradle.plugin.doxygen" version "1.2.0"
}

group "com.chmyaf.gradle.plugin"
version "1.3.0"
def publicationVcsUrl = "https://github.com/chmyaf/gradle-plugin-doxygen.git"

sourceCompatibility = 1.8

dependencies {
    compile gradleApi()
}

bintray {
    user = project.hasProperty("bintray.publish.user") ?
            project.getProperty("bintray.publish.user") :
            System.getenv("BINTRAY_PUBLISH_USER")
    key = project.hasProperty("bintray.publish.key") ?
            project.getProperty("bintray.publish.key") :
            System.getenv("BINTRAY_PUBLISH_KEY")
    configurations = ['archives']
    pkg {
        repo = project.group
        name = "gradle-plugin-doxygen"
        licenses = ["Apache-2.0"]
        vcsUrl = publicationVcsUrl
        version {
            name = project.version
            vcsTag = project.version
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    getArchiveClassifier().set("sources")
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

pluginBundle {
    website = 'https://chmyaf.com'
    vcsUrl = publicationVcsUrl
    tags = ['doxygen']
    description = 'Doxygen API documentation generator'

    plugins {
        gradlePluginDoxygen {
            id = "${project.group}.${project.name}"
            displayName = 'Generate source-code documentation with Doxygen'
        }
    }
}
